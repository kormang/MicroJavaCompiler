//import statements

package net.etfbl.kki;

import java_cup.runtime.*;
import rs.etf.pp1.symboltable.Tab;
import rs.etf.pp1.symboltable.concepts.*;

//action & parser code

parser code {:
	
	public void dbg(Object o){
		System.out.println(o);
	}
	
	public void report_error(String message, Object info){
		StringBuilder builder = new StringBuilder(message);
		if(info instanceof Symbol){
			builder.append(" in line ").append(((Symbol)info).left);
		}
		System.out.println(builder.toString());
	}
	
	/*
	public void report_fatal_error(String message, Object info) throws java.lang.Exception{
		done_parsing();
		report_error(message, info);
	}
	*/
	/*
	public void syntax_error(Symbol cur_token){
		report_error("\nSyntax error", cur_token);
	}
	*/
	/*
	public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception{
		report_fatal_error("\nFatal error!", cur_token);
	}
	*/

	public void report_info(String message, Object info){
		StringBuilder builder = new StringBuilder(message);
		if(info instanceof Symbol){
			builder.append(" in line ").append(((Symbol)info).left);
		}
		System.out.println(builder.toString());
	}

:}

scan with {:
	Symbol symbol = this.getScanner().next_token();
	if(symbol != null && symbol.value != null){
		System.out.println(symbol.toString() + " " + symbol.value.toString());
	}
	return symbol;
:}

init with {:

:}

//keyword terminals
terminal PROGRAM, PRINT, RETURN, VOID, CLASS, EXTENDS, NEW, IF, ELSE, WHILE, READ ;
terminal BREAK, DO, CONST ;

//operator terminals
terminal LOGICAL_OR, LOGICAL_AND, INCREAMENT, DECREAMENT, EQUAL, NOT_EQUAL, GREATER ;
terminal GREATER_OR_EQUAL, LESS, LESS_OR_EQUAL, PLUS, MINUS, ASSIGNMENT, TIMES, DEVIDE ;
terminal MOD, SEMI, COMMA, LPAREN, RPAREN, LBRACE, RBRACE, RBRACKET, LBRACKET, DOT ;

//literal terminals
terminal NUMBER_LITERAL, BOOL_LITERAL, CHAR_LITERAL, STRING_LITERAL ;

//other terminals
terminal IDENT, INVALID ;

//non-terminals:
non terminal Program, ProgParamDeclList, MethodDeclList, ProgParamDeclPart, ConstDecl, VarDecl, ClassDecl, ConstDeclNames, ConstDeclName ;
non terminal Literal, VarDeclList, VarDeclNames, VarDeclName, extendsStm, ClassMemberDeclList, ClassMemberDecl, MethodDecl, MethodDeclRest ;
non terminal FormPars;
non terminal FormParList, FormPar ;
non terminal Designator, Expr, Condition, Statement, StatementList, OtherStatement, OpenIf, ClosedIf ;
non terminal ActPars, CondTerm, CondFact, Relop, ExprRest, Addop, Term, Mulop, Factor ;
non terminal DesignatorRest, DesignatorRestPart ;
non terminal LoopHeader ;

non terminal Obj ProgName ;
non terminal Struct Type ;

//grammar

Program ::= PROGRAM ProgName:p ProgParamDeclList LBRACE {:parser.dbg("Program body");:} MethodDeclList RBRACE
{:
	parser.report_info("Program finished " + p, p);
	Tab.chainLocalSymbols(p);
	Tab.closeScope();
:};

ProgName ::= IDENT:pn {:
	parser.dbg("Program name is " + pn);
:}
;

ProgParamDeclList ::= ProgParamDeclList ProgParamDeclPart | /* epsilon */ ;
ProgParamDeclPart ::= ConstDecl | VarDecl | ClassDecl ;

ConstDecl ::= CONST Type ConstDeclNames SEMI ;
ConstDeclNames ::= ConstDeclNames COMMA ConstDeclName | ConstDeclName ;
ConstDeclName ::= IDENT:i ASSIGNMENT Literal:l {: parser.dbg("Declared Const " + i + " = " + l); :};


Literal ::= NUMBER_LITERAL | BOOL_LITERAL | CHAR_LITERAL | STRING_LITERAL ;

VarDeclList ::= VarDecl VarDeclList | /* epsilon */ ;
VarDecl ::= Type VarDeclNames SEMI ;
VarDeclNames ::= VarDeclNames COMMA VarDeclName | VarDeclName ;
VarDeclName ::= IDENT:i {:parser.dbg("Variable " + i +" is scalar");:} | IDENT:i LBRACKET RBRACKET{:parser.dbg("Variable " + i +" is array");:} ;

ClassDecl ::= CLASS IDENT:i {: parser.dbg("class " + i); :} extendsStm LBRACE {:parser.dbg("class body");:} ClassMemberDeclList RBRACE  ;
extendsStm ::= EXTENDS IDENT:i {: parser.dbg("extends " + i);:} | /* epsilon */ ;
ClassMemberDeclList ::= ClassMemberDecl ClassMemberDeclList | /* epsilon */ ;
ClassMemberDecl ::= VarDecl | MethodDecl ;

MethodDeclList ::= MethodDecl MethodDeclList | /* epsilon */;
MethodDecl ::= Type MethodDeclRest  | VOID MethodDeclRest;
MethodDeclRest ::= IDENT LPAREN FormPars RPAREN VarDeclList LBRACE {:parser.dbg("method body");:} StatementList {:parser.dbg("no more stms");:} RBRACE {:parser.dbg("end of meth body");:} ;

FormPars ::= FormParList | /* epsilon */ ;
FormParList ::= FormPar COMMA FormParList | FormPar ;
FormPar ::= Type IDENT ;


Type ::= IDENT:typeName
{:
	Obj typeNode = Tab.find((String)typeName);

	if(typeNode == Tab.noObj)
	{
		parser.report_error("No type " + typeName + " in symbol table.", null);
		RESULT = Tab.noType;
	}
	else{
		if(typeNode.getKind() == Obj.Type){
			RESULT = typeNode.getType();
		}
		else{
			parser.report_error("Error in line " + typeNameleft + " : Name " + typeName + " is not type name.", null);
			RESULT = Tab.noType;
		}
	}
:};

StatementList ::= StatementList Statement | /* epsilon */ ;

Statement ::= OpenIf {:parser.dbg("It's OpenIf");:} | ClosedIf {:parser.dbg("It's ClosedIf");:} ;

OpenIf ::= IF LPAREN Condition RPAREN Statement
		| IF LPAREN Condition RPAREN ClosedIf ELSE OpenIf
		| LoopHeader OpenIf
		;
		
ClosedIf ::= {:parser.dbg("other statement");:}OtherStatement
		 |	LPAREN Condition RPAREN ClosedIf ELSE ClosedIf
		 | LoopHeader ClosedIf
		 ;

OtherStatement ::= Designator ASSIGNMENT Expr SEMI
				| Designator INCREAMENT SEMI
				| Designator DECREAMENT SEMI
				| Designator LPAREN ActPars RPAREN SEMI
				| READ LPAREN Designator RPAREN SEMI
				| PRINT LPAREN Expr RPAREN SEMI
				| PRINT LPAREN Expr COMMA NUMBER_LITERAL RPAREN SEMI
				| BREAK SEMI
				| RETURN SEMI
				| RETURN {:parser.dbg("return");:} Expr SEMI
				| LBRACE StatementList RBRACE
				| DO Statement WHILE LPAREN Condition RPAREN SEMI
				| SEMI
				;

LoopHeader ::= WHILE LPAREN Condition RPAREN
			;


			
ActPars ::= ActPars COMMA Expr | Expr ;
Condition ::= Condition LOGICAL_OR CondTerm | CondTerm ;
CondTerm ::= CondTerm LOGICAL_AND CondFact | CondFact ;
CondFact ::= CondFact Relop Expr | Expr ;
Expr ::= MINUS ExprRest | ExprRest;
ExprRest ::= ExprRest Addop Term | Term ;
Term ::= Term Mulop Factor | Factor ;
Factor ::= Literal 
		| LPAREN Expr RPAREN
		| NEW Type
		| NEW Type LBRACKET Expr RBRACKET
		| Designator
		| Designator LPAREN ActPars RPAREN
		;
		
Designator ::= IDENT | IDENT DesignatorRest;
DesignatorRest ::= DesignatorRest DesignatorRestPart | DesignatorRestPart ;
DesignatorRestPart ::= LBRACKET Expr RBRACKET | DOT IDENT ;

Relop ::= EQUAL | NOT_EQUAL | GREATER | GREATER_OR_EQUAL | LESS | LESS_OR_EQUAL ;
Addop ::= PLUS | MINUS ;
Mulop ::= TIMES | DEVIDE | MOD ;










